\ FTYPE.hsf     tested for HSF2000
\ "FAST-TYPE" direct string to screen
\ FTYPE and F$. are used to print to the screen very quickly. 
\ They are NOT re-directed to other I/O devices like other HS/FORTH words.
\ IMPORTANT:  FTYPE prints strings only in the LISTS segment.
\     
\    They CAN be used with HS/FORTH windows since they start printing
\ at the current cursor position. They DO NOT however respect the window
\ boundaries. This can be overcome in a window by FTYPEing line by line
\ terminating each line with a CR. The speed improvement compared to
\ TYPE or $. is still excellent.
HEX


CODE <FTYPE> ( adr,len,vid-ram,offset,watrc -- )
              AH BL  MOV.                \ WATRC          -> AH 
              DI POP.                    \ screen postion -> DI
              ES POP.                    \ video ram adr  -> ES
              CX POP.                    \ string len     -> CX 
              DX SI MOV.                 \ temp store SI  -> DX
              SI POP.                    \ put string adr -> SI

@@ LOOP1.     LODSB.                     \ byte into AL reg
              STOSW.                     \ store AX reg to video
              LOOPCX LOOP1.              \ loop until CX=0

              SI DX MOV.                 \ restore SI from DX
              BX POP.                    \ POP the stack into BX
END-CODE

\ FTYPE  for direct to video ram screen update

1000 value page-size
  50 value scr-width

: ftype   ( adr,len -- )
     dup out+
     crt-base  cursor@ swap scr-width * + 2*  
     page#@ page-size * + 
     watrc <ftype> ;

: f$.    ( $addr -- )  count ftype ;

EXIT


\ Machine code for the above assembler code

CODE <FTYPE> 
      <% 88 DC 5F 07 59 89 F2 5E AC AB E2 FC 89 D6 5B %>
END-CODE


