\ ---[ Object Oriented Forth - Bernd Paysan ]------------------------
\ Mini-OOF                  12apr98py

: noop ;

: /string  ( adr c1 n -- adr2 c2 )
        tuck - >r + r> dup 0< IF  - 0  THEN ;

: method: ( m v -- m' v )
        Create  over , swap cell+ swap
        DOES> ( ... o -- ... ) @ over @ + @ execute ;

: var: ( m v size -- m v' )
        Create  over , +
        DOES> ( o -- addr ) @ + ;

: class: ( class -- class methods vars )
        dup 2@ ;


: end-class  ( class methods vars -- )
        Create  here >r , dup ,
                2 cells
                ?DO
                    ['] noop ,

                1 cells +LOOP
                cell+ dup cell+ r> rot @ 2 cells /string move ;

: defines ( xt class -- )
        ' >body @ + ! ;

: new ( class -- o )
        here over @ allot swap over ! ;

: :: ( class "name" -- )
        ' >body @ + @ compile, ;

Create object  1 cells , 2 cells ,
